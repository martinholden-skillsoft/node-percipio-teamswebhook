$.(
    $comment := '*******************************************************';
    $comment := '* Build the list of facts for the card.';
    $comment := '*     Duration in minutes if defined';
    $comment := '*     Locale Info';
    $comment := '*     Modalities';
    $comment := '*     Expertise';
    $comment := '*     Comma Delimited list of Authors from by';
    $comment := '*     ISBN from publication.isbn';
    $comment := '*     Copyright Publisher and date from publication.publisher';
    $comment := '*     and publication.copyrightYear';
    $comment := '* @returns [Object]';
    $comment := '*******************************************************';
    $getfacts := function() {(
        $facts := [];
        $facts := duration ? $append($facts, { 'name': 'Duration (Minutes)', 'value': $round($momentDuration(duration).asMinutes())}) : $facts;

        $facts := localeCodes[0] ? $append($facts, { 'name': 'Language', 'value': $languageInfo(localeCodes[0]).language.native}) : $facts;

        $facts := $count(modalities) > 0 ? $append($facts, { 'name': 'Modality', 'value': modalities[0]] }) : $facts;

        $facts := $count(expertiseLevels) > 0 ? $append($facts, { 'name': 'Expertise', 'value': expertiseLevels[0] }) : $facts;

        $facts := $count(by) > 0 ? $append($facts, { 'name': 'Authors', 'value' : $join(by, ', ') }) : $facts;

        $facts := publication ? publication.isbn ? $append($facts,{ 'name': 'ISBN',  'value' : publication.isbn }) : $facts : $facts;

        $publisher := publication ? publication.publisher ? publication.publisher : '' : '';
        $year := publication ? publication.copyrightYear ? $join(['(c)', $string(publication.copyrightYear)],' ') : '' : '';
        $copyright := publication ? $join([$publisher, $year],' ') : null;
        $facts := $copyright ? $append($facts,{ 'name': 'Publisher',  'value' : $copyright }) : $facts;

        $facts;
    )};

    $summaryTitle := $join(['New item added to Percipio "',$truncate($htmltotext(localizedMetadata[0].title), {'length': 255, 'omission': '...'}),'"']);

    $messagecard := (
        $.{
            '@type': 'MessageCard',
            '@context': 'https://schema.org/extensions',
            'summary': $summaryTitle,
            'title': $summaryTitle,
            'text': contentType.displayLabel,
            'sections': [
                {
                    'text': (
                        $join(['![',$summaryTitle,'](',imageUrl,'?width=400)'])
                    )
                },
                {
                    'text': (localizedMetadata[0].description ? localizedMetadata[0].description : '')
                },
                {
                    "facts": $getfacts()
                }
            ],
            'potentialAction': [
                {
                    '@type': 'OpenUri',
                    'name': 'Launch',
                    'targets': [
                        {
                            'os': 'default',
                            'uri': link
                        }
                    ]
                }
            ]
        }
    );

    $results := $messagecard;
    $results;
)